plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.8'
	id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.project'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'mysql:mysql-connector-java:8.0.33'
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	implementation 'com.fasterxml.jackson.core:jackson-databind'
	
	// OpenAPI 3 (Swagger) 의존성 추가
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.4.0'
	
	// .env 파일은 직접 읽기로 변경하므로 의존성 제거
	
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

tasks.named('test') {
	useJUnitPlatform()
}

// 프론트엔드 빌드 통합
task buildFrontend(type: Exec) {
    workingDir '../frontend'
    commandLine 'npm', 'run', 'build'
}

task copyFrontend(type: Copy) {
    dependsOn buildFrontend
    from '../frontend/dist'
    into 'src/main/resources/static'
}

// Python 스크립트 및 비디오 파일 복사
task copyPythonResources(type: Copy) {
    from '../beach_project'
    include 'simple_detection_linux.py'
    include 'requirements.txt'
    include 'yolov8n.pt'
    into 'src/main/resources/python'
}

task copyVideoResources(type: Copy) {
    from '../backend/videos'
    include '*.mp4'
    into 'src/main/resources/videos'
}

// 태스크 의존성 설정
processResources.dependsOn copyFrontend, copyPythonResources, copyVideoResources

bootJar {
    dependsOn copyFrontend, copyPythonResources, copyVideoResources
}
